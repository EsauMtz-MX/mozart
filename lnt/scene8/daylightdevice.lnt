module daylightdevice(config) with "get" is
(* daylight sensor device model *)
	process daylight [daylightevent:any, daylightaction:any, daylightenvaction:any, done:any] is
	  var q:QUEUE, daylighton:bool, element:QELEMENT, hasProperty:bool in
	  daylighton := false; q := nil;
	  element := elem(on, false);
		loop
			select
				(* emit event *)
				daylightevent(?any RULE, !daylight, !elem(on, daylighton), ?any bool) --any bool used to ignore irrelevant events based on rules
			[]
				(* push to action queue *)
				var elementAction:QELEMENT in 
					daylightaction(?any RULE, !daylight, ?elementAction of QELEMENT);
						q := append(elementAction,q)
				end var
			[]
				(* consume event from action queue *)
				eval hasProperty := queue_has_property(on, q, !?element);
				if (hasProperty) then 
					case element in 
						var onvalue:bool in 
						elem(on, onvalue) -> 
							daylighton := onvalue
						| any -> null
					end case;
					q := delete(element, q)
				end if
			[]
				(* handle action value from environment *)
				var elementAction:QELEMENT in 
					daylightenvaction(!daylight, ?elementAction of QELEMENT);
					case elementAction in 
							var onvalue:bool in 
							elem(on, onvalue) -> 
								daylighton := onvalue
							| any -> null
					end case
				end var
			[]
				(* used for track to one complete execution of the composition expression *)
				if empty(q) then
					done
				end if
			end select
		end loop
	  end var
	end process
end module